//
// ****************************************************************************
// * Copyright (C) 2016, International Business Machines Corporation          *
// * All rights reserved.                                                     *
// ****************************************************************************
//

use com.ibm.streamsx.monitoring.system::LogSource;
use com.ibm.streamsx.monitoring.system::*;

/**
 * This application tests the LogSource operator with applicationConfigurationName parameter
 *
 * @param applicationConfigurationName
 * Specifies the name of [https://www.ibm.com/support/knowledgecenter/en/SSCRJU_4.2.0/com.ibm.streams.admin.doc/doc/creating-secure-app-configs.html|application configuration object] that can contain domainId, connectionURL, user, password, and filterDocument properties. The application configuration overrides values that are specified with the corresponding parameters.
 *
 */
composite Main {
	param
		expression<rstring> $applicationConfigurationName: getSubmissionTimeValue("applicationConfigurationName", "com.ibm.streamsx.monitoring.LogSource.ApplicationConfiguration");

	graph

		/*
		 * The LogSource generates a tuple for each notified log event.
		 */
		(stream<LogNotification> Logs
		) = LogSource() {
			param
				applicationConfigurationName: $applicationConfigurationName;
		}

		/*
		 *  Verify the received notifications.
		 */
		(stream <rstring result> SaveDone1) as NotificationTracer = Custom(Logs as I) {
			logic 
			state: {
				mutable boolean done1Sent = false;
				mutable boolean errLogReceived = false;
				mutable boolean warnLogReceived = false;
			}
			onTuple I: {
				printStringLn((rstring)I);
				if (I.notifyType == "com.ibm.streams.management.log.application.error") {
					errLogReceived = true;
				}
				if (I.notifyType == "com.ibm.streams.management.log.application.warning") {
					warnLogReceived = true;
				}
				if ((!done1Sent) && (warnLogReceived) && (errLogReceived)) {
					submit({result="TEST_RESULT_PASS"}, SaveDone1);
					submit(Sys.WindowMarker, SaveDone1);
					done1Sent = true;
				}
			}
		}

		() as Done1 = FileSink(SaveDone1 as I) {
			param file: "done_1"; format: csv; flush: 1u; quoteStrings : false; writePunctuations: false; closeMode: punct; moveFileToDirectory: dataDirectory()+"/..";
		}

		stream<boolean dummy> Triggers as O = Beacon() {
			param
				period: 2.5;
		}
		
		() as Logger = Custom(Triggers as I) {
			logic
				state: {
					mutable int64 count = 0l;
				}
				onTuple I: {
					count++;
					appLog(Log.error, "This is error log #."+(rstring)count);
					block((float64)1ul);
					appLog(Log.warn, "This is warning log #."+(rstring)count);
				}			
		}

}
