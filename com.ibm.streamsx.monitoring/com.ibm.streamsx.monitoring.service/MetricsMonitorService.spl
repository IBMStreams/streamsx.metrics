//
// ****************************************************************************
// * Copyright (C) 2017, International Business Machines Corporation          *
// * All rights reserved.                                                     *
// ****************************************************************************
//

namespace com.ibm.streamsx.monitoring.service ;

use com.ibm.streamsx.monitoring.metrics::MetricsMonitor ;
use com.ibm.streamsx.monitoring.metrics::* ;
use com.ibm.streamsx.topology.topic::Subscribe ;
use com.ibm.streamsx.topology.topic::Publish ;
use com.ibm.streamsx.json::* ;

/**
 * The MetricsMonitorService ingests metric tuples from the MetricsIngestService and 
 * monitors the metric values against the threshold rules defined in the application 
 * configuration's thresholdDocument property.
 * 
 * Once a threshold is violated, an alert message is sent out, containing the details 
 * on the current metric value, the threshold rule that Was triggered. It also includes 
 * more details on the metric (from the tuple).
 * 
 * This composite depends on the following toolkits:
 * * **com.ibm.streamsx.topology**
 * * **com.ibm.streamsx.json**
 * 
 */
public composite MetricsMonitorService
{
	param
		/**
		 * The applicationConfigurationName allows the MetricsMonitorService to get access 
		 * to the application configuration.
		 */
		expression<rstring> $applicationConfigurationName : getSubmissionTimeValue("applicationConfigurationName", "thresholdDocumentConfiguration");
		
		/**
		 * The metricsSourceTopic specifies which topic the MetricsSourceService is publishing 
		 * to.
		 */
		expression<rstring> $metricsSourceTopic : getSubmissionTimeValue("metricsSourceTopic", "streamsx/monitoring/metrics/values");
		
		/**
		 * The metricsMonitorTopic specifies which topic this service (MetricsMonitorService) is 
		 * publishing to.
		 */
		expression<rstring> $metricsMonitorTopic : getSubmissionTimeValue("metricsMonitorTopic", "streamsx/monitoring/metrics/alerts");
		
	graph
		/**
		 * The Subscribe imports the metric tuples JSON from the topic  the MetricsSourceService's 
		 * is publishing to.
		 */
		stream<Json> JsonMetrics = Subscribe() {
			param
				topic : $metricsSourceTopic ;
				streamType : Json ;
		}
		
		/** The JSONToTuple converts the JSON back into Metrics tuples.		
		*/
		stream<Notification> Metrics = JSONToTuple(JsonMetrics) {
		}

		/**
		 * The MetricsMonitor compares the metric tuple's values against the threshold rules 
		 * defined in the application configuration's thresholdDocument property. It outputs 
		 * an alert whenever any threshold rules are violated.
		 */
		stream<rstring message> AlertMessage = MetricsMonitor(Metrics) {
			param
				applicationConfigurationName : $applicationConfigurationName ;
		}
		
		/** The TupleToJSON converts the alert message into JSON to prepare it for publishing.		
		*/
		stream<Json> JsonMessage = TupleToJSON(AlertMessage) {
		}

		/**
		 * The Publish publishes the alert messsages to the user-defined topic (default: 'streamsx/monitoring/metrics/alerts').
		 */
		() as Publish_0 = Publish(JsonMessage) {
			param
				 topic : $metricsMonitorTopic ;
		}

}



