//
// ****************************************************************************
// * Copyright (C) 2017, International Business Machines Corporation          *
// * All rights reserved.                                                     *
// ****************************************************************************
//

 
namespace com.ibm.streamsx.monitoring.metrics.services;

use com.ibm.streamsx.monitoring.metrics::*;
use com.ibm.streamsx.json::*;
use com.ibm.streamsx.topology.topic::Publish;

/**
 * Microservice that publishes metrics values notifications.
 * 
 * Invokes Invokes [com.ibm.streamsx.monitoring.metrics::MetricsSource] monitoring jobs and
 * publishes the metrics tuples with streams type [com.ibm.streamsx.monitoring.metrics::Notification] and in JSON format.
 * 
 * Output JSON Schema:
 * \{
 *   "domainId" : string,
 *   "instanceId" : string,
 *   "jobId" : string,
 *   "jobName" : string,
 *   "resource" : string,
 *   "peId" : long,
 *   "operatorName" : string,
 *   "channel" : int,
 *   "portIndex" : int,
 *   "connectionId" : string,
 *   "metricType" : string,
 *   "metricKind" : string,
 *   "metricName" : string,
 *   "metricValue" : long,
 *   "lastTimeRetrieved" : long
 * \}
 * 
 * This composite depends on the following toolkits:
 * * **com.ibm.streamsx.topology**
 * * **com.ibm.streamsx.json**
 * 
 * **Published topic**
 *
 * * `streamsx/monitoring/metrics/values` - Metrics value notifications
 *    with stream type [com.ibm.streamsx.monitoring.metrics::Notification].
 * 
 * @param monitoringConfiguration
 * Application configuration name for the JMX connection details. Defaults to `monitoring` and can be set using the submission parameter `monitoringConfiguration`.
 * The [https://www.ibm.com/support/knowledgecenter/en/SSCRJU_4.3.0/com.ibm.streams.admin.doc/doc/creating-secure-app-configs.html|application configuration object] can contain domainId, connectionURL, user, password, and filterDocument properties. The application configuration overrides values that are specified with the corresponding parameters.
 *
 * @param metricsTopic
 * The name of the topic this service is publishing to. If parameter is not set, then the topic name `streamsx/monitoring/metrics/values` is used.
 */
public composite MetricsIngestService
{			
	param
		expression<rstring> $monitoringConfiguration: getSubmissionTimeValue("monitoringConfiguration", "monitoring");
		expression<rstring> $metricsTopic: getSubmissionTimeValue("metricsTopic", "streamsx/monitoring/metrics/values");

	graph
		/**
		 * The MetricsSource generates a tuple for each retrieved metric.
		 */
		stream<Notification> MetricsIngest = MetricsSource() {
			param
				applicationConfigurationName: $monitoringConfiguration;
				emitMetricTuple: onChangedValue;
		}

		() as PublishMetrics = Publish(MetricsIngest) {
			param
				topic: $metricsTopic;
				allowFilter: true;
		}

		/**
		 * The TupleToJSON converts the Metrics tuples into JSON format.
		 */
		stream<Json> JsonMetricsIngest = TupleToJSON(MetricsIngest) {
		}
		
		/**
		 * The Publish publishes the JsonMetrics to the specified topic.
		 */
		() as PublishMetricsIngest = Publish(JsonMetricsIngest) {
			param
				topic: $metricsTopic;
		}
}
